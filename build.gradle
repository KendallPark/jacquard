// https://github.com/gradle-nexus/publish-plugin

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

repositories {
    mavenCentral()
}

group = 'com.spertus'
version = '0.1-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = 'Jacquard'
                description = 'Java autograder with Gradescope integration'
                url = 'https://github.com/espertus/jacquard'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'espertus'
                        name = 'Ellen Spertus'
                        email = 'ellen.spertus@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/espertus/jacquard.git'
                    developerConnection = 'scm:git:ssh://github.com:espertus/jacquard.git'
                    url = 'http://github.com/espertus/jacquard'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

dependencies {
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.25.3'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'org.junit.platform:junit-platform-launcher:1.10.0-M1'
    implementation 'net.sourceforge.pmd:pmd:7.0.0-rc3'
    implementation 'net.sourceforge.pmd:pmd-java:7.0.0-rc3'
    implementation 'org.jacoco:org.jacoco.core:0.8.10'
    implementation 'org.json:json:20230618'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0-M1'
    // Not used directly but needed by clients
    implementation 'com.puppycrawl.tools:checkstyle:10.12.1'
}

test {
    useJUnitPlatform()
}
