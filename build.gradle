// https://github.com/gradle-nexus/publish-plugin

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "de.undercouch.download" version "5.4.0"
    id "pmd"
}

repositories {
    mavenCentral()
}

group = 'com.spertus'
version = '0.2-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name = 'Jacquard'
                description = 'Java autograder with Gradescope integration'
                url = 'https://github.com/espertus/jacquard'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'espertus'
                        name = 'Ellen Spertus'
                        email = 'ellen.spertus@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/espertus/jacquard.git'
                    developerConnection = 'scm:git:ssh://github.com:espertus/jacquard.git'
                    url = 'http://github.com/espertus/jacquard'
                }
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

dependencies {
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.25.3'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'org.junit.platform:junit-platform-launcher:1.10.0-M1'
    implementation 'org.junit.platform:junit-platform-console:1.10.0-RC1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0-M1'
    implementation 'net.sourceforge.pmd:pmd:7.0.0-rc3'
    implementation 'net.sourceforge.pmd:pmd-java:7.0.0-rc3'
    implementation 'org.jacoco:org.jacoco.core:0.8.10'
    implementation 'org.json:json:20230618'
}

tasks.register('installCheckstyle', Download) {
    // If checkstyle version is upgraded, update constants in CheckstyleGrader.java.
    src 'https://github.com/checkstyle/checkstyle/releases/download/checkstyle-10.12.1/checkstyle-10.12.1-all.jar'
    dest 'lib'
    overwrite false
}

test {
    dependsOn(tasks.named("installCheckstyle"))
    useJUnitPlatform {
        excludeTags('IndirectTest')
    }
}

pmd {
    consoleOutput = true
    toolVersion = "6.52.0"
    rulesMinimumPriority = 5
    ruleSetConfig = resources.text.fromFile("config/pmd/project-rules.xml")
    ruleSets = []
}
